##––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––
## functioins for the library users
##––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––

# @brief adds options MARSLOGGING_${component_name}_ENABLE with value "true"
# and MARSLOGGING_${component_name}_Level with value "info"
function(marsLogging_add_component component_name)
  cmake_parse_arguments(PARSE_ARGV 1 ${_func_prefix} "" "${_option_func_keywords}" "")
  _check_and_warn_unpares_args()

  if(${${_func_prefix}_ENABLE})
    set(enable_value ${${_func_prefix}_ENABLE})
  else()
    set(enable_value true)
  endif()

  if(${${_func_prefix}_LEVEL})
    set(level_value ${${_func_prefix}_LEVEL})
  else()
    set(level_value info)
  endif()

  string(TOUPPER ${component_name} comp_upper)
  set(_ML_COMPONENT_LIST ${_ML_COMPONENT_LIST};${comp_upper} CACHE INTERNAL "")

  # add the two corresponding options
  set(MARSLOGGING_${comp_upper}_ENABLE ${enable_value} CACHE STRING "")
  set(MARSLOGGING_${comp_upper}_LEVEL ${level_value} CACHE STRING "")

  # add properties to the options, for easy usage with ccmake & cmake-gui
  set_property(CACHE MARSLOGGING_${comp_upper}_ENABLE PROPERTY
                                                      STRINGS ${_ML_VALID_ENABLE_NAMES})
  set_property(CACHE MARSLOGGING_${comp_upper}_LEVEL PROPERTY
                                                    STRINGS ${_ML_VALID_LEVEL_NAMES})

  # generate the temporary file, which is the source for ComponentConfig.h
  generate_temp_file()
endfunction()



##––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––
## helper functions and variables
##––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––––

###_internal_chached_constants____________________________________________________________
set(_ML_VALID_LEVEL_NAMES  "trace;debug;eval;info;warn;error;dev" CACHE INTERNAL
                           "The list of valid strings for log level names")
set(_ML_VALID_ENABLE_NAMES  "true;false" CACHE INTERNAL
                            "The list of valid strings for component_enable")

set(_ML_COMPONENT_LIST "" CACHE INTERNAL "")
set(_ML_COMPONENT_CONFIG_TEMP ${marsLogging_BINARY_DIR}/LoggingComponentConfig.h.in
                              CACHE INTERNAL "")
set(_ML_COMPONENT_CONFIG_HEADER ${marsLogging_BINARY_DIR}/LoggingComponentConfig.h
                                CACHE INTERNAL "")


###_local_constants_for_this_module_______________________________________________________
string(CONCAT _file_preamble
    "// This file is automatically generated by marsLogging.cmake\n"
    "// For each <component_name> passed to marsLogging_add_component() the following\n"
    "//   three variables are defined:\n"
    "//   * MARSLOGGING_<upper_case(<component_name>)>_ENABLE\n"
    "//   * MARSLOGGING_<upper_case(<component_name>)>_LEVEL\n"
    "//   * compConfig_<lower_case(<component_name>)>\n\n"
    "#ifndef _ML_PRINT_CONFIG_H_\n#define _ML_PRINT_CONFIG_H_\n")


###_utils_for_argument_parsing____________________________________________________________
set(_func_prefix ARG)
set(_option_func_keywords ENABLE LEVEL)

macro(_check_and_warn_unpares_args)
  if(${_func_prefix}_UNPARSED_ARGUMENTS)
  message(WARNING "marsLogging_add_component was called with unparsed arguments:\
      ${${_func_prefix}_UNPARSED_ARGUMENTS}")
  endif()
endmacro()


###_utility_functions_____________________________________________________________________
function(generate_temp_file)
  file(WRITE ${_ML_COMPONENT_CONFIG_TEMP} ${_file_preamble})
  foreach(component_name IN LISTS _ML_COMPONENT_LIST)
    string(TOLOWER ${component_name} comp_lower)
    generate_CXX_log_level(${component_name} cxx_style_log_level)
    string(CONCAT content
      "\n#define MARSLOGGING_${component_name}_ENABLE "
      "@MARSLOGGING_${component_name}_ENABLE@\n"

      "#define MARSLOGGING_${component_name}_LEVEL ${cxx_style_log_level}\n"

      "constexpr marsLogging::ComponentConfig compConfig_${comp_lower}"
      "{MARSLOGGING_${component_name}_ENABLE,\n    "
      "MARSLOGGING_${component_name}_LEVEL}\;\n")
    file(APPEND ${_ML_COMPONENT_CONFIG_TEMP} ${content})
  endforeach()

  file(APPEND ${_ML_COMPONENT_CONFIG_TEMP} "\n#endif  // _ML_PRINT_CONFIG_H_")
  configure_file(${_ML_COMPONENT_CONFIG_TEMP} ${_ML_COMPONENT_CONFIG_HEADER})
endfunction()


function(generate_CXX_log_level component_name return_var)
  set(level_value ${MARSLOGGING_${component_name}_LEVEL})
  if(${level_value} IN_LIST _ML_VALID_LEVEL_NAMES)
    set(${return_var} "marsLogging::LogLevel::@MARSLOGGING_${component_name}_LEVEL@"
                  PARENT_SCOPE)
  elseif(${level_value} GREATER_EQUAL 0 AND ${level_value} LESS_EQUAL 255)
    set(${return_var}
        "static_cast<marsLogging::LogLevel>(@MARSLOGGING_${component_name}_LEVEL@)"
        PARENT_SCOPE)
  else()
    string(CONCAT error_msg
        "\"MARSLOGGING_${component_name}_LEVEL\"=${MARSLOGGING_${component_name}_LEVEL} "
        "is neither a valid level name as defined in _ML_VALID_LEVEL_NAMES"
        "=[${_ML_VALID_LEVEL_NAMES}] nor a valid 8 bit unsigned integer.")
    message(FATAL_ERROR ${error_msg})
  endif()
endfunction()
