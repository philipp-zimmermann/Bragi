##▁0▁INITIATE▁PROJECT▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
cmake_minimum_required(VERSION 3.14)

string(CONCAT project_desc "Minimal logging framework. The main focus is "
       "zero runtime overhead, while being threadsafe and convenient to use.")
project(marsLogging
        VERSION 2.0.0
        LANGUAGES CXX
        DESCRIPTION ${project_desc})



##▁1▁DETERMINE▁ENVIRONMENT▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
if(PROJECT_SOURCE_DIR STREQUAL marsLogging_SOURCE_DIR)
  set(_ML_IS_MAIN_PROJECT 1)
endif()



##▁2▁DEPENDENCIES▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")



##▁3▁OPTIONS▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
set(_ML_VALID_LEVEL_NAMES  "trace;debug;info;warn;error;dev" CACHE INTERNAL
                           "The list of valid strings for log level names")
set(MARSLOGGING_GLOBAL_ENABLE ON CACHE BOOL
    "enables/disables printing of messages with marsLogging. Overrides all other levels")
set(MARSLOGGING_GLOBAL_LEVEL "info" CACHE STRING
                             "Defines the lowest level of printed log messages.")
set_property(CACHE MARSLOGGING_GLOBAL_LEVEL PROPERTY STRINGS ${_ML_VALID_LEVEL_NAMES})

# include Module for option adding here



##▁4▁CONFIGURE▁PROJECT▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
set(_ML_INCLUDE_DIR ${marsLogging_SOURCE_DIR}/include) # this will change when installing
set(_ML_SOURCE_DIR ${marsLogging_SOURCE_DIR}/src)
set(_ML_PRINT_CONFIG_DIR ${marsLogging_BINARY_DIR})


##▁5▁EXPORTED▁TARGETS▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
add_library(marsLogging_config INTERFACE)
target_include_directories(marsLogging_config INTERFACE ${_ML_INCLUDE_DIR}
                                                        ${_ML_SOURCE_DIR}
                                                        ${_ML_PRINT_CONFIG_DIR})
target_compile_features(marsLogging_config INTERFACE cxx_std_14)



##▁6▁EXAMPLES▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
if(_ML_IS_MAIN_PROJECT)
  add_subdirectory(bin/examples)
endif()



##▁7▁TESTS▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
if(_ML_IS_MAIN_PROJECT)
  add_subdirectory(bin/tests)
endif()



##▁8▁INSTALL▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
##▁9▁PACKAGE▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁▁
